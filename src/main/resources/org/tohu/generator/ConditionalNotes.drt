template header
itemID
label
style
questionID
condition
 
package org.tohu.generator;

 /*
  * This package has been generated using Tohu Generator
  * Do not modify by hand if you wish to regenerate
  */
  
import org.tohu.Questionnaire;
import org.tohu.Group;
import org.tohu.Question;
import org.tohu.MultipleChoiceQuestion;
import org.tohu.Note;

template "Conditional-Notes"

rule "Conditional_Note_Create_@{itemID}_@{row.rowNumber}"
dialect "mvel"
	when
		not Note ( id == "@{itemID}" )
		Question ( id == "@{questionID}", answered == true, textAnswer == "@{condition}" )
		Group ( items == null || items not contains "@{questionID}" )
	then
		Note note = new Note("@{itemID}", "@{label}");
		note.setPresentationStyles({"@{style}"});
		insert(note);	
end

rule "Insert_Conditional_Expanded_Tip_@{itemID}_@{row.rowNumber}"
dialect "mvel"
	when
		$question : Question( id == "@{questionID}" )
		$group : Group (items != null && items contains "@{questionID}" && items not contains "@{itemID}")
		$note : Note( id == "@{itemID}", (presentationStyles != null && presentationStyles contains "expandedTip") )
	then
		$group.appendItem($note.getId(), $question.getId());
		update ($group);
end

rule "Conditional_Note_Retract_@{itemID}_@{row.rowNumber}"
dialect "mvel"
	when
		$question : Question( id == "@{questionID}", answered == true, textAnswer != "@{condition}" )
		$group : Group (items != null && items contains "@{questionID}" && items contains "@{itemID}")
		$note : Note( id == "@{itemID}" )
	then		
		$group.removeItem( "@{itemID}" );
		update( $group );		
		retract($note);
end
		

end template